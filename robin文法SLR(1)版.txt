program              → declarations EOF

declarations         → declaration declarations | ε

declaration          → funDecl | varDecl | statement 

statement            → breakStmt | continueStmt | returnStmt | exprStmt | ifStmt | whileStmt | repeatStmt | block

breakStmt            → "break" ";"

continueStmt         → "continue" ";"

returnStmt           → "return" exprStmt

funDecl              → identifier "=" "function" "(" paramList ")" block

varDecl              → type varDef varDefs ";"

varDef               → var varInit

varDefs              → "," varDef varDefs | ε

varInit              → "=" expression | ε

type                 → "integer" | "float" |"string" | "bool" | "char" | "var"

exprStmt             → expression ";" | ";"

ifStmt               → "if" "(" expression ")" statement elseBranch

elseBranch            → "else" statement | ε

whileStmt            → "while" "(" expression ")" statement

block                → "{" declarations "}"

repeatStmt           → "repeat" statement "until" "(" expression ")" ";"

paramList            → parameter parameters | ε

parameters           → "," parameter parameters| ε

parameter            → type var

expression           → assignment

assignment           → var "=" assignment | logic_or

logic_or             → logic_and logic_or_prime

logic_or_prime       → "or" logic_and logic_or_prime | ε

logic_and            → equality logic_and_prime

logic_and_prime      → "and" equality logic_and_prime | ε

equality             → comparison equality_prime

equality_prime       → "!=" comparison equality_prime | "==" comparison equality_prime | ε

comparison           → term comparison_prime

comparison_prime     → ">" term comparison_prime | ">=" term comparison_prime | "<" term comparison_prime | "<=" term comparison_prime | ε

term                 → factor term_prime

term_prime           → "-" factor term_prime | "+" factor term_prime | ε

factor               → incr_exp factor_prime

factor_prime         → "/" incr_exp factor_prime | "*" incr_exp factor_prime | "%" incr_exp factor_prime | ε

incr_exp             → unary incr_op

incr_op              → "++" incr_op | "--" incr_op | ε

unary                → "!" unary | "-" unary | call

call                 → primary call_suffix

call_suffix          → "(" argList ")" call_suffix | ε

argList              → expression arguments | ε

arguments            → "," expression arguments| ε

primary              → const | "(" expression ")" | var

const                → "true" | "false" | "nil" | int | real | string

var                   → identifier varSuffix

varSuffix          → "[" expression "]" varSuffix | ε


